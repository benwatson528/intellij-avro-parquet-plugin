plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    //Avro dependencies
    compile("org.apache.avro:avro:1.10.2") {
        exclude group: 'org.slf4j'
    }
    compile("org.xerial.snappy:snappy-java:1.1.8.4")

    //Parquet dependencies
    compile("org.apache.parquet:parquet-avro:1.12.0")
    compile("org.apache.parquet:parquet-column:1.12.0")
    compile("org.apache.parquet:parquet-hadoop:1.12.0")
    compile("org.apache.parquet:parquet-format-structures:1.12.0")
    compile("org.apache.hadoop:hadoop-client:3.3.0")

    //External dependencies
    compile("com.google.code.gson:gson:2.8.6")
    compile("com.google.guava:guava:23.6-jre")
    compile("com.fifesoft:rsyntaxtextarea:3.1.1")
    compile("com.github.wnameless.json:json-flattener:0.11.1")

    //Test dependencies
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    testCompile("org.assertj:assertj-core:3.18.1")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

intellij {
    updateSinceUntilBuild = false
    version '2017.1.6'
    patchPluginXml {
        sinceBuild("171")
        changeNotes """
      Now works on M1 Macs."""
    }
}

buildSearchableOptions.enabled = false

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/benwatson528/intellij-avro-parquet-plugin")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifact file("build/distributions/intellij-avro-parquet-viewer-${project.version}.zip")
        }
    }
}

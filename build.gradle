plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    //Avro dependencies
    compile("org.apache.avro:avro:1.9.2") {
        exclude group: 'org.slf4j'
    }
    compile("org.xerial.snappy:snappy-java:1.1.7.3")

    //Parquet dependencies
    //This is org.apache.parquet:parquet-avro-1.11.0 with added support for INT96
    compile files("libs/parquet-avro-1.12.0-SNAPSHOT.jar")
    compile("org.apache.parquet:parquet-column:1.11.0")
    compile("org.apache.parquet:parquet-hadoop:1.11.0")
    compile("org.apache.parquet:parquet-format-structures:1.11.0")
    compile("org.apache.hadoop:hadoop-client:3.2.1")

    //External dependencies
    compile("com.google.code.gson:gson:2.8.6")
    compile("com.fifesoft:rsyntaxtextarea:3.1.0")
    compile("com.github.wnameless.json:json-flattener:0.8.1")

    //Test dependencies
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.6.0")
    testCompile("org.assertj:assertj-core:3.17.1")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

patchPluginXml {
    sinceBuild("171")
    changeNotes """
      Some LogicalTypes are not written as valid JSON, which was causing some files to not be displayed at all. This update ensures that such files can still be viewed in the raw pane."""
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/benwatson528/intellij-avro-parquet-plugin")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifact file("build/distributions/intellij-avro-parquet-viewer-${project.version}.zip")
        }
    }
}

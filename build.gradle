plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    //Avro dependencies
    compile("org.apache.avro:avro:1.10.0") {
        exclude group: 'org.slf4j'
    }
    compile("org.xerial.snappy:snappy-java:1.1.8.1")

    //Parquet dependencies
    //This is org.apache.parquet:parquet-avro-1.11.0 with added support for INT96
    compile files("libs/parquet-avro-1.11.1-SNAPSHOT.jar")
    compile("org.apache.parquet:parquet-column:1.11.1")
    compile("org.apache.parquet:parquet-hadoop:1.11.1")
    compile("org.apache.parquet:parquet-format-structures:1.11.1")
    compile("org.apache.hadoop:hadoop-client:3.3.0")

    //External dependencies
    compile("com.google.code.gson:gson:2.8.6")
    compile("com.fifesoft:rsyntaxtextarea:3.1.1")
    compile("com.github.wnameless.json:json-flattener:0.11.1")

    //Test dependencies
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.7.0")
    testCompile("org.assertj:assertj-core:3.18.1")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

intellij {
    updateSinceUntilBuild = false
    version '171'
    patchPluginXml {
        sinceBuild("171")
        changeNotes """
      Reverting to Java 8 to ensure that as many people as possible can run the plugin."""
    }
}

buildSearchableOptions.enabled = false

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/benwatson528/intellij-avro-parquet-plugin")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifact file("build/distributions/intellij-avro-parquet-viewer-${project.version}.zip")
        }
    }
}
